plugins {
    id 'org.springframework.boot' version '2.3.12.RELEASE'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
}

group = 'borman'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
    mavenCentral()
}

dependencies {

    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    implementation group: "com.twilio.sdk", name: "twilio", version: "8.14.0"

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

test {
    useJUnitPlatform()
}


task copyClient(dependsOn: ":app-client:clientBuild") {

    doFirst {
        delete "src/main/resources/static"
        delete "src/main/resources/templates"
    }

    doLast {
        copy {
            from new File(project(":app-client").getProjectDir(), "build")
            into "src/main/resources/static"
        }
        copy {
            from new File(project(":app-client").getProjectDir(), "build/index.html")
            into "src/main/resources/templates"
        }
        copy {
            from new File(project(":app-client").getProjectDir(), "build/error.html")
            into "src/main/resources/templates"
        }
    }
}

task finalizeBuild(type: Copy, dependsOn: build) {
    from "${buildDir}/libs/app-server-${version}.jar"
    into "${buildDir}/libs"
    rename "app-server-${version}.jar", "aroundthehorn.jar"
    delete "app-server-${version}.jar"
}

processResources.dependsOn(copyClient)
clean.dependsOn(":app-client:clientClean")
build.finalizedBy(finalizeBuild)

task stage(dependsOn: ['build', 'clean'])
build.mustRunAfter clean